import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

group 'dev.markusk'
version '0.0.1'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    api 'com.apptastic:rssreader:2.2.1' //https://github.com/w3stling/rssreader
    api 'org.jsoup:jsoup:1.12.1'
    api 'com.google.code.gson:gson:2.8.6'
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    api group: 'commons-codec', name: 'commons-codec', version: '1.14'

    /*Logging and console*/
    api 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'org.jline:jline:3.10.0'
    api group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
    api group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
    /*--Logging and console--*/

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task sourcesForRelease(type: Copy) {
    from 'src/main/java'
    into 'build/filteredSrc'
    filter(ReplaceTokens, tokens: [VERSION: version, DATE: getBuildDate(), TIME: getBuildTime()])
}

static def getBuildDate() {
    return new Date().format('yyyy/MM/dd')
}

static def getBuildTime() {
    return new Date().format('HH:mm:ss')
}

compileJava {
    options.encoding = 'UTF-8'
    source = sourcesForRelease.destinationDir
    classpath = sourceSets.main.compileClasspath
    dependsOn sourcesForRelease
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from "${buildDir}/filteredSrc"
}

jar {
    archivesBaseName = project.name
}

shadowJar {
    archiveClassifier.set('withDependencies')
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter sourcesJar
}
